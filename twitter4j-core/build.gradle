plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group 'org.twitter4j'
version '4.0.8-SNAPSHOT'

repositories {
    mavenCentral()
}
sourceSets {
    main {
        java {
            srcDirs(['src/internal-async/java',
                     'src/internal-http/java',
                     'src/jmx/java',
                     'src/internal-json/java',
                     'src/internal-logging/java',
                     'src/internal-util/java',
                     'src/stream/java',
                     'src/main/java'])
        }
    }
}

task sourcesJar(type: Jar) {
    duplicatesStrategy = 'include'
    classifier = 'sources'
    from sourceSets.main.allSource
}

java {
    withJavadocJar()
    withSourcesJar()
}
compileJava   {
    sourceCompatibility = '8'
    targetCompatibility = '8'
}

compileTestJava   {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

dependencies {
    compileOnly 'org.slf4j:slf4j-api:2.0.0'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.18.0'
    implementation 'org.jetbrains:annotations:23.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.seleniumhq.selenium:htmlunit-driver:3.64.0'
    testImplementation 'com.googlecode:kryo:1.04'
}
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

test {
    useJUnitPlatform()
}

jar {
    from('module-info/build/classes/java/main') {
        include "module-info.class"
        into "META-INF/versions/9"
    }
    manifest {
        attributes 'Multi-Release': 'true'
    }
}
tasks.jar.dependsOn([':twitter4j-core:module-info:compileJava'])

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'twitter4j-core'
            from components.java
            pom {
                name = 'twitter4j-core'
                description = 'twitter4j'
                url = 'https://github.com/Twitter4J/Twitter4J'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yusuke'
                        name = 'Yusuke Yamamoto'
                        email = 'yusuke@mac.com'
                    }
                }
                scm {
                    url = 'https://github.com/Twitter4J/Twitter4J/'
                    connection = 'scm:git:git://github.com/Twitter4J/Twitter4J.git'
                    developerConnection = 'scm:git:git@github.com:Twitter4J/Twitter4J.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.properties['SONATYPE_USERNAME'] ?: "none"
                password = project.properties['SONATYPE_PASSWORD'] ?: "none"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
